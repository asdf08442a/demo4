configurations {
    mbgen
}
dependencies {
    compile project(':common')
    mbgen('org.mybatis.generator:mybatis-generator-core:1.3.6')
    mbgen('mysql:mysql-connector-java:6.0.6')
    mbgen project(':common')
}

def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/mbgen/config.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties
}

project.task('mbgen', group: "tool", description: "利用MyBatis代码生成工具生成数据表的映射代码").doLast {
    file("$projectDir/src/gen/java").mkdirs()
    file("$projectDir/src/gen/resources").mkdirs()

    def properties = getDbProperties()
    ant.properties['jdbc_driverClassname'] = properties.getProperty("jdbc.driverClassName")
    ant.properties['jdbc_url'] = properties.getProperty("jdbc.url")
    ant.properties['jdbc_username'] = properties.getProperty("jdbc.username")
    ant.properties['jdbc_password'] = properties.getProperty("jdbc.password")
    ant.properties['src_gen_java'] = projectDir.path + '/src/gen/java'
    ant.properties['src_gen_resources'] = projectDir.path + '/src/gen/resources'
    ant.properties['package_model'] = properties.getProperty("package.model")
    ant.properties['package_mapper'] = properties.getProperty("package.mapper")
    ant.properties['package_xml'] = properties.getProperty("package.xml")
    ant.taskdef(
            name: 'mbgen',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mbgen.asPath
    )
    ant.mbgen(overwrite: true,
            configfile: projectDir.path + '/src/main/resources/mbgen/mbgen.xml',
            verbose: true) {
        propertyset {
            propertyref(name: 'jdbc_driverClassname')
            propertyref(name: 'jdbc_url')
            propertyref(name: 'jdbc_username')
            propertyref(name: 'jdbc_password')
            propertyref(name: 'src_gen_java')
            propertyref(name: 'src_gen_resources')
            propertyref(name: 'package_model')
            propertyref(name: 'package_xml')
            propertyref(name: 'package_mapper')
        }
    }
}